plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'com'
version = '2.0.2'
jar.enabled = false

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "ProtocolLib"
        url = "https://repo.dmulloy2.net/repository/public/"
    }
    maven {
        name = "NoteBlockAPI"
        url = "https://jitpack.io"
    }
    maven {
        url = "https://maven.enginehub.org/repo/"
    }
    maven {
        url = 'https://repo.codemc.io/repository/maven-public/'
    }
}
dependencies {
    compileOnly group: "com.comphenix.protocol", name: "ProtocolLib", version: "4.7.0"
    compileOnly "io.papermc.paper:paper-api:1.20.1-R0.1-SNAPSHOT"
    compileOnly 'com.github.koca2000:NoteBlockAPI:1.6.2'
    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.9'
    implementation 'org.bstats:bstats-bukkit:3.0.2'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    archiveFileName = "${project.name} ${project.version}.jar"
    configurations = [project.configurations.runtimeClasspath]
    relocate 'org.bstats', 'com.slimer.bstats'
}

build {
    dependsOn shadowJar
}
